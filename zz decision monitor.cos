* ASSUMPTION:
* ov90/91/92 aren't used for anything in norns
inst
*make a new agent with fmly/gnus numbers that no one is using (probably)
	new: simp 10 10 5800 "blnk" 1 0 0
* run every tick, (we'll try to process 1 creature every tick)
	tick 1
*move to the norn meso
	mvto 281 8736
endm

scrp 10 10 5800 9
* open a file for output in the world journal directory
	FILE OOPE 0 "decision history.log" 1
* enumerate over all creatures
	enum 4 0 0
* if it has a genome
		doif crea targ
*ensure the decision doesn't change while we're running
			inst
			setv va00 decn
			addv va00 1
 different decision than last time we looped
			doif va00 ne ov90 OR attn ne ov91
				setv ov90 va00
				seta ov91 attn
				addv ov92 1
*output the moniker
				outs gtos 0
*output a tab
				outs "\t"
*output the time
				outs rtif rtim "%Y-%m-%d %H-%M-%S"
*output a tab
				outs "\t"
*output the name of the creature
				outs hist name gtos 0
*output a tab
				outs "\t"
*output the catagory name of the targeted agent
				outs catx attn
*output a tab
				outs "\t"
*output the decision number
				outv ov92
*output a tab
				outs "\t"
*output the decision neuron
				outv decn
*output a newline
				outs "\n"
			endi
*wait for the next tick to avoid lag
			slow
		endi
	next
*close the output file, and actually write the changes to disk.
	file oclo
endm

